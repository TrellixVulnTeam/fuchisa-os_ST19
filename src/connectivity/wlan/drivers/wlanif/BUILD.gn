# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/driver_package.gni")

all_hdrs = [
  "convert.h",
  "device.h",
  "driver.h",
]

all_src = [
  "convert.cc",
  "device.cc",
  "driver.cc",
]

all_deps = [
  ":wlanif_bind",
  "//garnet/lib/wlan/protocol",
  "//sdk/banjo/ddk.hw.wlan.wlaninfo",
  "//sdk/fidl/fuchsia.wlan.device",
  "//sdk/fidl/fuchsia.wlan.internal",
  "//sdk/fidl/fuchsia.wlan.mlme",
  "//sdk/fidl/fuchsia.wlan.mlme:fuchsia.wlan.mlme_c_client",
  "//sdk/fidl/fuchsia.wlan.mlme:fuchsia.wlan.mlme_c_server",
  "//src/connectivity/wlan/lib/common/cpp:common",
  "//src/devices/lib/driver",
  "//src/lib/ddk",
  "//zircon/public/lib/async-cpp",
  "//zircon/system/ulib/async-loop:async-loop-cpp",
  "//zircon/system/ulib/async-loop:async-loop-default",
]

source_set("lib_source") {
  public = all_hdrs
  sources = all_src
  public_deps = all_deps
}

bind_rules("wlanif_bind") {
  rules = "wlanif.bind"
  output = "wlanif-bind.h"
  deps = [ "//src/devices/bind/fuchsia.wlan" ]
}

driver_module("wlanif_driver") {
  output_name = "wlanif"

  deps = [ ":lib_source" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
}

driver_package("wlanif") {
  deps = [ ":wlanif_driver" ]

  drivers = [
    {
      name = "wlanif.so"
    },
  ]
}
